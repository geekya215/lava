plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'org.graalvm.buildtools.native' version '0.9.14'
    id 'jacoco'
}

group 'io.geekya215'
repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    implementation 'org.jline:jline-terminal-jansi:3.21.0'
    implementation 'org.jline:jline-reader:3.21.0'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("--enable-preview")
}

tasks.withType(JavaExec).configureEach {
    jvmArgs("--enable-preview")
}

tasks.withType(Test).configureEach {
    jvmArgs("--enable-preview")
}

shadowJar {
    archiveBaseName.set('lava')
    archiveClassifier.set('')
    archiveVersion.set('')
}

jar {
    manifest {
        attributes('Main-Class': 'io.geekya215.lava.Main')
    }
}

graalvmNative {
    binaries {
        main {
            imageName = 'lava'
            mainClass = 'io.geekya215.lava.Main'
            verbose = true
            buildArgs.add("-J--enable-preview")
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = false
    }
}

check.dependsOn jacocoTestReport